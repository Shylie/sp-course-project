0000	COPY	START	0		.COPY FILE FROM INPUT TO OUTPUT
		EXTDEF	BUFFER,BUFEND,LENGTH		
		EXTREF	RDREC,WRREC		
0000	FIRST	STL	RETADR	172027	.SAVE RETURN ADDRESS
0003	CLOOP	+JSUB	RDREC	4B100000	.TEST FOR RECORD
0007		LDA	LENGTH	032023	.TEST FOR EOF LENGTH = 0)
000A		COMP	#0	290000	
000D		JEQ	ENDFIL	332007	.EXIT IF EOF FOUND
0010		+JSUB	WRREC	4B100000	.WRITE OUTPUT RECORD
0014		J	CLOOP	3F2FEC	.LOOP
0017	ENDFIL	LDA	=C'EOF'	032016	.INSERT END OF FILE MARKER
001A		STA	BUFFER	0F2016	
001D		LDA	#3	010003	.SET LENGTH = 3
0020		STA	LENGTH	0F200A	
0023		+JSUB	WRREC	4B100000	.WRITE EOF
0027		J	@RETADR	3E2000	.RETURN TO CALLER
002A	RETADR	RESW	1		
002D	LENGTH	RESW	1		.LENGTH OF RECORD
		LTORG			
0030	*	=C'EOF'		454F46	
0033	BUFFER	RESB	4096		.4096-BYTE BUFFER AREA
1033	BUFEND	EQU	*		
1000	MAXLEN	EQU	BUFEND-BUFFER		
0000	RDREC	CSECT			
	.
	.	SUBROUTINE TO READ RECORD INTO BUFFER
	.
		EXTREF	BUFFER,LENGTH,BUFEND		
0000		CLEAR	X	B410	.CLEAR LOOP COUNTER
0002		CLEAR	A	B400	.CLEAR A TO ZERO
0004		CLEAR	S	B440	.CLEAR S TO ZERO
0006		LDT	MAXLEN	77201F	
0009	RLOOP	TD	INPUT	E3201B	.TEST INPUT DEVICE
000C		JEQ	RLOOP	332FFA	.LOOP UNTIL READY
000F		RD	INPUT	DB2015	.READ CHARACTER INTO REGISTER A
0012		COMPR	A,S	A004	.TEST FOR END OF RECORD (X'00')
0014		JEQ	EXIT	332009	.EXIT LOOP IF EOR
0017		+STCH	BUFFER,X	57900000	.STORE CHARACTER IN BUFFER
001B		TIXR	T	B850	.LOOP UNLESS MAX LENGTH HAS BEEN REACHED
001D		JLT	RLOOP	3B2FE9	
0020	EXIT	+STX	LENGTH	13100000	.SAVE RECORD LENGTH
0024		RSUB		4F0000	.RETURN TO CALLER
0027	INPUT	BYTE	X'F1'	F1	.CODE FOR INPUT DEVICE
0028	MAXLEN	WORD	BUFEND-BUFFER	000000	
0000	WRREC	CSECT			
	.
	.	SUBROUTINE TO WRITE RECORD FROM BUFFER
	.
		EXTREF	LENGTH,BUFFER		
0000		CLEAR	X	B410	.CLEAR LOOP COUNTER
0002		+LDT	LENGTH	77100000	
0006	WLOOP	TD	=X'05'	E32012	.TEST INPUT DEVICE
0009		JEQ	WLOOP	332FFA	.LOOP UNTIL READY
000C		+LDCH	BUFFER,X	53900000	.GET CHARACTER FROM BUFFER
0010		WD	=X'05'	DF2008	.WRITE CHARACTER
0013		TIXR	T	B850	.LOOP UNTIL ALL CHARACTER HAVE MEEN WRITTEN
0015		JLT	WLOOP	3B2FEE	
0018		RSUB		4F0000	.RETURN TO CALLER
		END	FIRST		
001B	*	=X'05'		05	
